;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;  HTTP.LSP - An object-oriented http server written in CLisp.
;;;
;;;  Copyright (c) 1999 by Erann Gat, all rights reserved.
;;;
;;;  This program may be used and distributed in accordance with the
;;;  GNU General Public Licence (GPL) as published by the Free Software
;;;  Foundation (www.fsf.org)
;;;
;;;  Please send comments, improvements, and bug reports to Erann Gat
;;;  <gat@jpl.nasa.gov>.
;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;  General notes:
;;;
;;;  This program really ought to be in three files, but I ran into
;;;  a problem with CLisp (which has since been fixed) that caused me
;;;  to merge them together into one to get things to work.  Some day
;;;  I'll unmerge them.  For now, you should be aware that there are
;;;  three major sections in this file.  The first is a set of general
;;;  utilities that I use in all my programs.  These include some
;;;  top-level macros that I use for defining classes and methods.
;;;  These macros are:
;;;
;;;  (DEFINE-CLASS class-name &rest slot-specs)
;;;
;;;  where a slotspec is either just a slot name or a list (slot-name
;;;  initial-value).
;;;
;;;  (DEFINE-METHOD (method-name (self-var classname &rest slot-names)
;;;                              &rest args)
;;;                 &body body)
;;;
;;;  DEFINE-METHOD is syntactic sugar for DEFMETHOD and WITH-SLOTS.
;;;
;;;  Some other weird things I use a lot: RECEIVE is a synonym for
;;;  MULTIPLE-VALUE-BIND.  ITERATE is like named LET.
;;;
;;;  All these things come from long ago when I was a fan of T and
;;;  OakLisp.
;;;
;;;  The second part of the file is the server proper.
;;;
;;;  The general idea behind the design of this http server is that
;;;  interacting with it should be fundamentally an interaction with
;;;  Lisp objects.  Instead of a read-eval-print loop it's a
;;;  submit-request - eval - general-html loop.  I'm sure I have not
;;;  realized that vision in the best possible way, but it's not
;;;  bad either.
;;;
;;;  The third part of the file is a set of examples.  This is probably
;;;  the best place to start.
;;;
;;;  Quickstart:
;;;
;;;  1.  Load this file.  Then compile it and load it again.  (I haven't
;;;      gone through to put eval-whens in the right place to allow it to
;;;      compile without first being loaded.)
;;;  2.  Call serve.  Serve takes an optional port number. The defult is
;;;      1234.
;;;
;;;  This code has been tested with the 1999-07-22 version of CLisp.
;;;  It will probably not run under earlier versions.  (This code
;;;  depends on several functions that had bugs in earlier versions.)
;;;

(in-package :user)



;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;  General utilities
;;;
(defun concatenate-symbol (&rest symbols)
  (let ( (*print-case* (readtable-case *readtable*)) )
    (declare (special *print-case* *readtable*))
    (intern (format nil "窿簌礅镬螬┅ㄤ彐磲泸轸弪狒钺礤狎珞蝈篝怙澌啜灬忮祗è钺礤磲疸狎＇汜狎珞棱镤┅ì钺礤括磲疸狎＇汜潋狎珞┅┅ㄤ彐磲泸溴骈铄簌铒铢蟊蟛啜箦翩簌礅镬骢钽糸镱К蟊ㄦ躅泗轱蟛┅ㄤ彐躅簌斫蟊蟛ㄡ钿簌礅镬蟊簌礅镬蟛篝蜷铉簌礅镬钺礤蟊簌礅镬钺礤蟛┅┅ㄤ彐磲泸骖ㄡ蜱怙澌怙澌啵Ж灬礅溽狎珞ㄤ邈灬蝈ㄩ珙矧徕戾泪蜱螬棱镤┅ㄤ彐躅桢瞟ㄦ矧磲ｘ丌瞟鲠祯弩┅ㄤ彐躅篑í┅ㄤ彐躅蝮é蝈篝铛礅弪螬绎雉镦翳篚镦翳篑踽蝈戾蝈篚祠癌ㄤ镬轶铛礅弪螬ㄩ钽蝈篚祠í瞟┅篑螋蝈篚祠┅ㄤ彐磲泸溴戾翦翳轭痨徙蝈篝狎珞啜箦翩痨徙ㄤ屐弭翳轭痨徙泪蜱螬┅ㄤ彐磲泸麒殪ㄣ镱溟糸镱怙澌怙澌戾翎ㄧ孱簌⑻谅盘┅啜翎玮镤翎麒孱泔钿轸轱棱镤ㄧ翎绌┅┅ㄤ彐磲泸躅糸ㄣ镱溟糸镱怙澌怙澌戾翎ㄧ孱簌⑻谅盘┅啜翎玮镤翎躅戾篌泔钿轸轱棱镤ㄧ翎绌┅┅换换换换换换换换换换换换换锰嫌豸殪轸殄换ㄤ彐磲泸溴骈铄沆狍钺礤蝈篝箪雉螬戾ㄩㄩㄡ麸钺礤钺礤ㄣ狎钺礤┅啜痱镧ㄤ彐沆狍殇ㄩㄡ麸钺礤Жㄣ潋钺礤┅磲疸狎＇灬礅溽箪雉ㄩㄡ钿ㄣ镱箴箪雉ㄣ滗箪雉┅ㄥ蝌矧⒃镲磲铢轸屙轭箪雉箴邈殒殂狒轱詈英箪雉┅戾钺礤ㄩ簌礅镬箪雉箪雉ㄦ轵篝箪雉┅ㄩ铋翩矧ㄩ簌礅镬箪雉铋箦泔钿箪雉┅啜钺礤洪铋翎蜱ㄩ铘弪簌礅镬钺礤钺礤щ妁黠蜾横沣弩箫ㄣ镱汜翦钺翦簌礅镬殇Л钺礤洪铋翩矧轭轸骘蝽┅箪雉螬ㄤ彐躅ㄣ镱汜翦钺翦簌礅镬ы犭瀛殇é蝈篝狎珞ㄡ痧禊＇磲脲轭篝犷沐К殇狎珞┅ㄤ彐躅ㄣ镱汜翦钺翦簌礅镬殇Эㄡ蜱豉疱狎К殇┅К殇┅ㄤ彐躅屮趄徙舡溴沆狎狒轱铙ㄢ镤ㄩ翦蜥翦祜镳ㄤ邈灬蜥糸镱铋飑ㄢ镤怙澌ㄩㄡ钿ㄣ镱箴怙澌ㄣ镱箴ㄣ狎怙澌┅ㄥㄣ後怙澌т邈灬蝈┅祜镳ㄣ镱ㄣ狎怙澌溴沆狎狒轱铙ㄣ潋怙澌┅鲠祯弩溴沆狎狒轱铙怙澌┅┅ㄤ彐磲泸溴骈铄礤翳镤è镳弪狒轱箦戽狎豉疱蝈篝轭篝犷沐鲠蝮蝈篝狎珞怙澌怙澌眭祠轲戾鲠祯瀛忾钿ㄤ邈灬蜥糸镱怙澌ㄥ趄徙舡溴沆狎狒轱铙怙澌啜溴骓弭栾括殒ㄡ麸镳弪狒轱瞟扉篝镳弪狒轱瞟镳弪狒轱瞟è箦戽狎豉疱泪蜱螬冷邈灬蜥糸镱ㄩ轭篝犷沐鲠蝮啜鏖翳箪雉轭篝犷沐鲠蝮箦戽狎棱镤啜痱镧棱镤┅┅ㄤ彐磲泸溴骈铄痱轭舡礤翳镤è沆狍蝈篝轹狎螬蝈篝狎珞啜溴骈铄礤翳镤痱轭舡镡赍泗箦戽沆狍篱鲠蝮篝蝈犴ㄦ矧磲篝蝈犴泪蜱螬┅ㄤ彐磲泸溴骈铄篝犷溽蜾痱轭舡礤翳镤ㄣ灬篌啜溴骈铄痱轭舡礤翳镤ì沆狍螬＜埘铗邈镧铋箦窿脲黠蜾镢沲蝌邃桢蝈狎珞紊梯ｘ鼐К沆狍ē徜潋弩蟓镦箦戽┅换换换换换换换换换换换换换歪痧弪换ㄤ彐轭瀛簌铒铢麽祀磲疸ㄤ彐躅磲鸨ㄦ磲痧邃狎蝈篝躅磲痧邃狎珞磲疸狎＇灬礅溽ㄡ蜱ㄡ痧禊骖狎躅磲痧邃狎珞┅磲痧邃狎绌ㄤ彐躅麽祀ㄦ磲痧邃狎蝈篝躅磲痧邃狎珞磲疸＇灬礅溽ㄡ蜱ㄡ痧禊骖狎躅磲痧邃狎珞┅磲痧邃狎绌ㄤ彐躅磲痨遽鲥ㄦ趄邋ㄩ翦蜥翦祜镳趄邋趄邋ㄩㄡ麸趄邋ㄦ躅汜祆骖趄邋ㄣ镱祜镳ㄣ狎趄邋┅ㄡ钿ㄣ潋趄邋祜镳ㄣ潋趄邋┅┅┅ㄤ彐躅磲痨遽鲥蟆ㄦ趄邋ㄩ翦蜥翦祜镳趄邋趄邋ㄩㄡ麸趄邋ㄦ躅汜祆骖趄邋痱镧箦翩ㄣ狎趄邋祜镳ㄣ狎趄邋┅箦翩ㄣ潋趄邋ㄡ钿ㄣ潋趄邋祜镳ㄣ潋趄邋┅┅趄邋┅┅ㄤ彐躅磲痧孱ㄦ蝈篝扉篝螬ㄡ痧禊＇狃疱钿ㄡ痧禊＇磲疸狎骖扉篝螬┅ㄤ彐轭瀛簌铒铢磲痧孱洹磲疸犷ㄤ彐躅磲痧孱浔ㄦ蝈篝扉篝螬ㄡ痧禊＇狃疱钿ㄡ痧禊＇磲鸨骖扉篝螬┅ㄤ彐轭瀛簌铒铢磲疸潋磲痨轶舂ㄤ彐轭瀛簌铒铢麽祀沅磲痨ㄤ彐躅磲稹ㄦ飑麽祀沅＇灬礅溽飑箦翩ㄣ狎飑ㄦ躅汜祆骖ㄣ狎飑┅飑ㄤ彐磲泸磲痨弭ㄢ轭溟铉怙澌怙澌啜磲疸狎ㄦ磲疸狎＇汜忾钿轭珞棱镤括磲疸狎＇箦泔钿忾钿轭珞┅ㄤ彐磲泸麽祀戾ㄢ轭溟铉怙澌怙澌啜麽祀ㄦ磲疸狎＇汜忾钿轭珞棱镤括磲疸狎＇箦泔钿忾钿轭珞┅ㄤ彐磲泸瞽镦ㄦ矧瞟戾ㄣ秕铘鲠ㄧ孱簌⑽┅啜轸弪狒祜镳ì泔躅赧狎瞟ㄩ弪镳泔躅赧狎铋ㄣ镱骘蝽祜镳ū泔躅赧狎┅┅┅换换换换换换换换换换换换鱼桢礤篝蹑换ㄤ彐磲泸蝈沐轹鲠蝮骘蝽怙澌怙澌ㄩㄡ麸鲠蝮啜戾ì鲠蝮眭祠轲戾鲠祯瀛扉篝骘蝽┅棱镤啜眭祠轲戾鲠祯瀛忾钿鲠蝮骘蝽棱镤┅ㄤ彐磲泸忾钿ㄢ轭溟铉怙澌怙澌戾翦眇鲠蝮瞽镦ㄧ孱簌⒃磐孝戾铉翳忾钿轭珞┅啜戾磲疸狎＇扉篝翦眇鲠蝮磲疸狎＇汜忾钿轭珞┅躅鏖钿痱雉邈痱镧括磲疸狎ㄦㄢ轭溟铉ㄣ镱箦翩忾钿轭绌忾钿轭珞棱镤括磲疸狎ㄦㄢ轭溟铉翦眇鲠颟啜箦翩ㄣ狎忾钿轭绌翦眇鲠颟忾钿轭珞翦眇鲠蝮┅┅ㄤ彐磲泸翥镱é蝈篝沆狨箦螬ㄩ翦蜥翦祜镳ㄣ灬躞弩沆狨箦螬戾ㄣ灬躞ㄣ狎沆狨箦螬ㄣ镱铛祆沆狨箦螬铋簌斫箦泔钿沆狨箦Ы京ㄡ篌弪戾铉翳沆狨箦畅ī⑸祆彗犰孟文沆狨箦英沆狨箦戾翦眇鲠ㄧ孱簌⒃磐孝┅啜戾ì翦眇鲠ㄣ狎沆狨箦┅ㄩ翦眇鲠ㄦ躅汜祆ㄣ徜潋沆狨箦翦眇鲠颟祜镳ㄣ潋沆狨箦螬┅┅铛祆ㄣ潋沆狨箦┅啜矧ㄣ狎沆狨箦祜镳ㄣ潋沆狨箦螬┅啜殒ㄣ狎沆狨箦痱镧括沅沆狨箦┅祜镳ㄣ潋沆狨箦螬┅┅┅ㄤ彐轭瀛簌铒铢羼羼ㄤ彐轭瀛簌铒铢铛祆铛祆ㄤ彐轭瀛簌铒铢弪锟弪镳ㄤ彐轭瀛簌铒铢铛礅弪铛礅弪皓ㄤ彐轭瀛簌铒铢狒镯狒镯ㄤ彐轭瀛簌铒铢痱镢邃躜蹇骢钽糸镱皓换换换换换换换换换换悦豸轸殪殄换＋兔痱镧ㄤ彐躅镳孱沆殄铘箫汶弭ㄨ矬痫螋ㄣ沆汉镳孱翥瓠篝蝈犴栾篝痫螋┅ㄤ彐躅泔铑邈糸镱篝狒螬ㄣ沆汉镳孱趄犷箴矧舡篝蝈犴泔铑邈糸镱篝狒螬ㄤ彐躅镳孱箦蝣弪箫汶弭痫螋戾ㄣ沆汉镳孱翥瓠篝蝈犴铋痫螋┅躅鏖钿痱雉邈痱镢弩蟓麽轸⒆衢糸铉骘泔铑邈糸镱ㄦīㄥㄣ镱铄泗轱瞽篝狒螬轰狒狲驽颟┅躅戾篌ㄥㄣ镱铄泗轱瞽篝狒螬轰狒狲驽颟ㄣ祜箦螬┅螬＋锰捎ㄩ眇矧簌篝屙汉骈铛眇ㄤ彐鲠箦蝣弪篝蝈犴铋飑ㄤ彐鲠梏麴趄徙濯铋飑ㄤ彐鲠梏麴蝻雉镡赍泗ㄤ彐鲠筢驽蝈徜翎忪濯ㄣ镳蝈徜翎忪濠ㄤ镬轶ㄣ扉篝＼＼┅箦舡簌铘狲骝镯汨狎＼筢驽蝈徜翎忪濯┅换换换换换换换换换澡麸瓠戾鲥骢钽糸镱换＋兔ㄤ彐躅箦蝣é镳糸镱犰痫螋辈炒┅祜镳戾镳孱箦蝣弪箫汶弭痫螋┅痱镢弩蟓蝓瞽骢钽糸镱Ⅲ弪鲥颌箦蝣瀛箫汶弭螬┅＋锰捎ㄤ彐躅箦蝣é镳糸镱犰痫螋辈炒┅戾箦蝣弪箫汶弭箦蝣弪痫螋┅躅鏖钿痱雉邈祜镳箦蝣瀛箫汶弭箫汶弭徙沐痿箦蝣弪┅箫汶弭箦蝣弪沆矬箦蝣弪┅鲠祯弩┅换换换换换换换换换仍孕豸殪轸殄换ㄤ彐躅梏麴痱轭é蝈篝轸屙螬戾箦蝣弪篝蝈犴ㄤ镬轶ㄩ翦轸屙螬ㄩ篝蜷铉轸屙痱轭轸屙螬痱轭ㄨ趑瓠孱泔溴扉翦蜥飙篝蜷铉痱轭悱麸篝蜷铉轸屙┅螬┅痱轭＼蝈趱蝾螬痱轭＼扉铄驽邃螬┅＋兔ㄤ彐躅梏麴蝈徜扉铄īㄣ沆汉翦祛弭蝈徜扉铄箦蝣弪篝蝈犴┅＋锰捎ㄤ彐躅梏麴蝈徜扉铄ī戾舄蝈徜扉铄箦蝣弪篝蝈犴┅戾ū戾铉翳螬┅ㄩㄡ钿窘戾癌ㄥ耢ㄣ栳戾瞟＼靡┅箦翩篚怏羼戾瞟┅螬ㄤ彐躅梏麴孱泔溴扉翦蜥飙篝蜷铉螬躅戾篌篝蜷铉螬箦翩痱轭悱麸篝蜷铉螬┅鏖翳秕麴豸麸篝蜷铉秕舂ㄤㄩū椹窘戾铉翳螬秕戾ㄣㄣ栳椹痱轭ㄣ狍ǎ芗祠虎ǎ芫玺雉桢蝼轶悌秕舂┅┅ㄤ彐磲泸筢驽禊é怙澌怙澌啜戾í蝈徜翎忪濯筢驽蝈徜翎忪濯ㄤ邈灬蝈箴邈獒蝈徜翎忪濯┅蝈沐轹鲠弪颟ㄩ珙矧瀛弪蝻蝮痱镧棱镤┅ㄩ豉疱弪с镱溟糸镱弪鲠飑┅ㄤ彐躅疳蝮瀛梏麴桢徜弪ㄨ戾ㄩ钿屮痫箝糸镱＼瑭ㄡ钿轭溴扉篝ㄩ铘弪篝蜷铉躔汜箦篚怏羼轭溴┅篚怏羼ǐ轭溴博┅┅ㄤ彐躅疳蝮瀛躜螬戾舄ㄩ钿屮痫箝糸镱＼羽徙螬ㄩ钿屮痫箝糸镱＼羽徙烘蝻憝孱舂ㄡ钿轭溴轭溴轭溴轭溴暴戾礤翳镤ㄩ铘弪篚怏羼轭溴暴┅躜篚怏羼ǐ轭溴博轭溴博ㄦ矧憝箪雉麒孱ㄡ钿ㄥ礤翳镤х弭箦翩轭溴痫箝糸镱＼躜飑┅箦翩骘蝽箪雉篚怏羼躜ū轭溴暴┅箦翩躜篚怏羼躜轭溴暴┅鲠祯弩躜疳蝮瀛骘蝽箪雉骘蝽箪雉螬┅┅ㄤ彐躅疳蝮瀛骘蝽箪雉螬戾舄ㄩ钿屮痫箝糸镱＼螬ㄩ钿屮痫箝糸镱＼螬ㄣ镱轭溴ㄣ镱ㄩ铘弪篝蜷铉躔汜箦篚怏羼轭溴暴┅ㄣ镱疳蝮瀛躜飙孱泔溴洵篝蜷铉篚怏羼ū轭溴暴轭溴博ㄡ钿轭溴疳蝮瀛骘蝽箪雉篚怏羼ū轭溴博┅┅篝蜷铉铋螬┅ㄤ彐躅疳蝮瀛躜飙孱泔溴洵篝蜷铉螬鏖翳秕麴豸麸篝蜷铉秕舂ㄤㄩū椹窘戾铉翳螬秕戾ㄣㄣ栳椹ㄣ狍ǎ堀箦翩ㄣ镤瀛汨狎疳蝮瀛轭翦珏后翎螋ǐ暴哄钿ǐ畅候徜轼倍宏躅氕犰祜麇舂┅躅戾篌矧铛祆悌ㄥ耢＼扉铄驽邃┅痱轭秕舂ㄩ钽博ǎ塬痱轭＼羽徙秕舂雉桢蝼轶痱轭秕舂┅┅┅ㄤ彐躅屙轸梏麴痱遽礅戾īㄨ趑瓠痱轭⑷栽携碑舶纤ㄨ趑瓠痱轭⒚镱翦铘赠疱翦舣梏盱ㄨ趑瓠痱轭ㄨ趑瓠痱轭⒓怙澌忡泔祜蚪堍Ｆ破破栖⒕剪轸戾驹弩箦蝣弪集糸綮寰艰尘┅换换换换换换换换换仍孕钺礤箴徙换换澡轶箬秕熹痱镡徕禊忮蝈黩轸翦麸篝矧仍孕忾钿轭珞镱翳换簌礅镬痱镳弪豉扉篝换ㄤ彐鲠梏麴钺礤箴徙濯Ж┅ㄤ彐磲泸溴骈铄梏麴钺礤鲠祯濠啜箦翩ㄧ弭梏麴钺礤箴徙濯К钺礤鲠祯濠ㄤ彐躅珏舡梏麴镡赍泗钺礤ㄧ弭梏麴钺礤箴徙濯钺礤┅换换换换换换换换换换燥瓠戾鲥箦蝣弪祜镳换ㄤ彐躅箦蝣瀛箫汶弭螬躅鏖钿痱雉邈戾í箦蝣弪篝蝈犴螬躜骘蝽箪雉ㄤ邈灬蝈箴邈獒箦蝣弪篝蝈犴┅ㄥ黹舡梏麴痱遽礅戾清翳找箦翩躜ㄨ趑瓠蝈徜扉铄┅嗅蝮翳找眭祠轲戾鲠祯瀛箦赳躜骘蝽箪雉螬疳蝮瀛躜躜飑清翳桢徜弪ㄩ珙矧邃骘铒鳗祜镳骘桢徜弪疳蝮瀛梏麴桢徜弪ㄨ趑瓠蝈徜扉铄┅麒殪桢徜弪狃疱钿桢徜弪清翳桢徜弪骝镯邢釉镳弪狒轱ㄩ铛祆骘蝽箪雉螬箦翩骘蝽箪雉祜镳麒殪扉篝孱螬狃疱钿疳蝮瀛骘蝽箪雉ㄨ趑瓠蝈徜扉铄┅┅麒孱梏麴趄徙濯痱轭扉篝躜骘蝽箪雉螬┅澡轶轶翳桢狎镦翳箦蝣弪戾镡ㄩ篝蜷铉躜鸿趑瓠蝻雉镡赍泗筢驽禊蝈徜骝镯篝蜷铉躜飑┅涉翳找轶铒簌礅镬牾篝弼犰踽翦轸ㄩ铒簌礅镬镡戛箦翩镡筢驽禊ㄥ鲠镡戛┅涉翳找轶簌礅镬珏翳梏麴镡赍泗怙躅麸翳簌礅镬痱镧箦翩镡ㄧ弭梏麴镡赍泗镡戛ㄩ铛祆镡戛箦翩镡ㄨ趑瓠铒舡骘躅躜飑┅换涉翳镡赍泗轶痱镢邃躜瀣屮邈豸轸雉桢蝼轶换牾篝躞翳镡赍泗轸箦戽ㄩ痱镢邃躜蹇镡戛箦翩镡筢驽禊ㄦ躅汜祆镡骘蝽箪雉螬┅┅柔钿戾弪蝻蝮桢蝈ㄩ豉疱镡с镱溟糸镱箦翩镡ㄨ趑瓠弪蝻镡戛┅ㄩ梏麴趄徙濯痱轭镡戛彭轸翳蝈篚祠ㄨ趑瓠屙轸镡戛清铄蜥翦箫礤翳轭骘紊箫麇滹瞌珏睐篝弪轱躞忪犷疳珏ㄩ铛祆镡戛ㄨ趑瓠痱轭⒓零糸镱蝈趱蝾邃紊叹┅ㄦ轭轶璀秕麴豸螬ㄣ祜箦螬┅ㄤ彐躅梏麴弪蝻ㄣ镱溟糸镱扉篝⑴乙弦泔钿轸轱瞟ㄤ彐躅梏麴铒舡骘躅躜飑扉篝⒋按蜗葡瘴募芯澡找秕蝈聃弩翦洮躜泔蹯铒忮骘躅洚┅换换换换换换换换换换换仍吞轶镡赍泗换ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸扉篝┅ㄤ镬轶ㄩ飑ㄨ趑瓠屙轸椹┅ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸翳轭舂ㄨ趑瓠痱轭翳轭绌ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸ㄣ泔钿轸轱瞟ㄨ趑瓠痱轭⑴乙弦⒓芯┅换换换换换换换腮疱蜢轭换ㄤ彐轭瀛沆狍棂疱蜢轭翦躜飑ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸ㄨ棂疱蜢轭翦躜飑ㄨ趑瓠痱轭ㄦ矧磲铋⒓栩彐杰淋⒕良峋躜翦舂┅换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换骑蝽换ㄤ彐轭瀛沆狍骘蝽礤翳镤ю矬舂ㄢ豸麸瞽灬忮篚忭轸徙糸镱轸屙蝈箦舡怩趑镱咯ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸ㄦ骘蝽礤翳镤徙糸镱轸屙怩趑镱灬忮蝈箦舡怩趑镱咯ㄨ趑瓠痱轭⒓葡彝团匀夏舰礤翳镤撩陨衔舰徙糸镱⒕ㄤ镬轶ㄩ翦轸屙螬ㄨ趑瓠屙轸轸屙┅ㄨ趑瓠痱轭⒓晌姓再信接章蜕至陶沤堍怩趑镱灬忮④⒕ㄩ蝈箦舡怩趑镱ㄨ趑瓠痱轭⒓晌姓再信揭庞旁劲┅ㄨ趑瓠痱轭⒓葡彝劲┅换换换换换换换换湾铛ㄦ矧轸屙换ㄤ彐轭瀛沆狍梏盱礤铛殇轸屙溴驷蹯舂ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸梏盱礤铛殇轸屙溴驷蹯舂ㄨ趑瓠痱轭⒓优膛迷瘟团杰殇④⒕ㄤ镬轶ㄩ翦轸屙螬ㄨ趑瓠痱轭⒓闲陨衔ㄩㄥ轸屙溴驷蹯舂ㄨ趑瓠痱轭⒂盘琶耘┅ㄨ趑瓠痱轭⒅撂张杰轸屙④⒕轸屙┅ㄨ趑瓠痱轭⒓优膛迷劲┅换换换换换换换换藻怙ㄦ矧轸屙换ㄤ彐轭瀛沆狍翦舡怙殇箝磲戾铉翳疳篌黠蜾咯ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸翕翦舡怙殇箝磲戾铉翳疳篌黠蜾咯ㄨ趑瓠痱轭⒓晌姓再信舰ㄩ疳篌黠蜾ю狍篦矧翦舂瘟团舰殇ㄩ箝ㄨ趑瓠痱轭由谂舰箝濠ㄩ磲戾铉翳ㄨ趑瓠痱轭土靥盼窃冉磲戾铉翳┅ㄨ趑瓠痱轭⒕┅换换换换换换换换藻狎遽ㄩ瀹忾翦怙ㄦ矧轸屙换ㄤ彐轭瀛沆狍翦舡狎遽殇蝻黧泔祗溴驷蹯舡翦黩狃舂ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸翎翦舡狎遽殇蝻黧泔祗溴驷蹯舡翦黩狃咯ㄨ趑瓠痱轭⒓耘卦烈帕瘟团舰殇ㄩ蝻黧ㄨ趑瓠痱轭蚁子舰蝻黧┅ㄩ泔祗ㄨ趑瓠痱轭孟逃舰泔祗┅ㄩ黩狃ㄨ趑瓠痱轭⒆伊孝┅ㄨ趑瓠痱轭⒕ㄩ溴驷蹯舡翦ㄨ趑瓠痱轭溴驷蹯舡翦舂ㄨ趑瓠痱轭⒓耘卦烈帕劲┅换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换葬忪弩换ㄤ彐轭瀛沆狍翎忪糸綮泔铘孱趔ㄢ矧溴蝼殇翳暴ㄢ矧溴蜚镬矧癌ㄤ彐轭瀛礤翳镤ㄨ趑瓠屙轸翕翎忪糸綮泔铘孱趔怙蜾弪鏖漪怙蜾弪泔祜颟ㄨ趑瓠痱轭⒓粤绿孪夷乓舰怙蜾弪鏖漪孪夷乓孟滔医怙蜾弪泔祜⒕ㄩ糸綮ㄨ趑瓠痱轭⒓昧性上尉糸綮⒓昧性上尉┅ㄤ镬轶蝻泔铘孱趔ㄨ趑瓠痱轭⒓砸炯匀劲ㄣ狎蝻鳗ㄤ镬轶ㄩ翦ㄣ潋蝻鳗ㄨ趑瓠痱轭⒓阅劲ㄨ趑瓠屙轸轸屙┅ㄨ趑瓠痱轭⒓粤绿啪┅换换换换换换换栳钿豸殪轸换ㄤ彐磲泸鏖翳骘蝽箪雉ㄡ蜱鲠箪雉钺礤怙澌怙澌啜戾ì括祜镳骘箪雉钺礤轭箪雉钺礤泔祆邈啜箪雉钺礤ㄧ弭狎琏狎К箪雉钺礤┅┅棱镤┅换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换换砒犴痨换ㄤ彐轭瀛梏麴鸿趑瓠蝻雉镡赍泗扉篝⒆屐泔礤麸翳锰轶梏麴箦蝣弪屮犴痨疳珏〖芯⑿戾狍祜轭杭鹁磲脲骘蝽横泗轱鲥蜷纟疳篌黠蜾涕铍麸铄镡赍泗洪翦眢扉篝⒄箦赡磲脲翦舡怙洪躞弪殇⒓芯嗅篌黠蜾磲脲翦舡怙洪ю狍篦矧吼狍篦矧淇舂⒓芯┅┅ㄤ彐轭瀛梏麴鲥蜷纟疳篌黠蜾ㄦㄡ蜱螬鏖翳骘蝽箪雉狎珞躞弪殇疳篌黠蜾ㄤ邈灬蝈ㄩ珙矧疳篌黠蜾┅扉篝⒆屐泔礤躞弪殇⒓芯磲脲箦戾泗矧骘蝽躞弪殇┅┅ㄤ彐鲠盹铘梵Ж赆驽磲狃磲牾牾狨箦痿镢铒溴悌ㄤ彐躅磲脲溽翦箦戾泗矧ī蝈沐轹溽翦盹铘遽颟ㄤ邈镤瀛躅轹弪筢飙糸礤ㄧ弭躅轹弪筢飙糸礤┅ㄤ邈灬蝈ㄩ珙矧瑭扉篝磲脲梏盱礤铛洪ы镱翳洪翦眢盹铘梵轰彐狨祠铘ū盹铘瑭盹铘梵┅磲脲梏盱礤铛洪т狴洪翦眢祜镳骘骝镯麸潮泔祆邈椹轰彐狨祠溽翦磲脲梏盱礤铛洪遽洪翦眢祜镳骘骝镯遽麸ǐ遽旦泔祆邈椹轰彐狨祠遽颟┅ㄤ彐躅磲脲箦戾泗矧骘蝽ㄩ洎磲脲骘蝽横泗轱т轶痨狴筱桢漉戾衡豸麸瞽灬忮⒅殄筱桢漉戾洪翦眢扉篝⒓芯渝戾泗溽翦磲脲溽翦箦戾泗矧⒓晌姓再信饺赡呐瘟团秸优噎赡至陶沤殇⒕夹劲┅ㄤ彐轭瀛梏麴溟箴灬筱桢漉戾ㄦㄡ蜱螬鏖翳骘蝽箪雉狎珞盹铘溽遽躞弪殇扉篝⒓门卧乓居汨邃蹯镦犰泔铈弪孱沐蝻镯骘盹铘溽遽⒓芯磲脲筱桢漉扉铉骘蝽ū痫箝糸镱ㄩ铘弪篝蜷铉躔汜箦盹铘瑭盹铘梵┅疳蝮瀛轭翦珏溽疳蝮瀛轭翦珏遽颟⒓瘸炯芯渺殂镱犷尖踞鲠殪徕戾集饩箪雉麸箝珙躔骘蝻镯磲脲箦戾泗矧骘蝽躞弪殇┅┅ㄤ彐躅磲脲筱桢漉扉铉骘蝽磲脲翎忪恒镱翦铘ㄣ镱扉篝⒁镲雹⒁镲并⒁镲尝祜镳骘糸礤骝镯麸渤泔祆邈ㄣ镱ㄦ矧磲铋梁鞍糸礤祜镳骘蝻镯轭Ж蝻镯蝻镯蝻镯博泔祆邈磲脲棂疱蜢轭呼屮⒘至商谅膛乎蜢啜箝珙躔К蝻镯糸礤┅┅┅┅ㄤ彐躅箝珙躔蝻镯糸礤扉篝ㄦ矧磲铋①秕狎铒箝珙邃躔骘镱连狒良鹁蝻镯铘盹铘梵糸礤⑷弪濮箫礤雉桢屮犴痨篝蹑婧拣劲⑿戾狍汨镲箦镱镦翳骘祆秣轭镳糸镱蠛拣劲磲脲棂疱蜢轭呼屮⑾痿轱雹乎蜢㈣疱蜢轭氡⒓芯磲脲棂疱蜢轭呼屮⑾痿轱并乎蜢ㄨ疱蜢轭氩辈畅磲脲骘蝽横泗轱ф矧肀衡豸麸瞽灬忮躔溽翦洪翦眢扉篝⒂屐邈箫礤镳糸镱蠛磲脲梏盱礤铛洪ы孱醣洪翦眢Ж旦轰彐狨祠畅磲脲梏盱礤铛洪ы孱醪洪翦眢Ж姗⒓芯蓬翦箫礤翦艉磲脲翦舡怙洪翦舯⒓芯蓬翦疳篌黠蜾孩磲脲翦舡怙洪ю狍篦矧浔吼狍篦矧淇舂⒓芯磲脲翦舡狎遽洪翦舡狎遽候秣轰彐狨祠翦⑷屐祜┅磲脲翎忪恒镱翦铘Ж蝻鞅畅蝻鞑订蝻鞒卑北┅磲脲翎忪恒镱翦铘啜ㄦ矧眢磲脲骘蝽横泗轱ф矧聿衡豸麸瞽灬忮躔溽翦洪翦眢扉篝⑵矧聿┅磲脲骘蝽横泗轱ф矧沓衡豸麸瞽灬忮躔溽翦洪翦眢扉篝⑵矧沓┅磲脲骘蝽横泗轱ф矧泶衡豸麸瞽灬忮躔溽翦洪翦眢扉篝⑵矧泶┅┅┅花溴骈铄梏麴棂疱蜢轭氡⒃栝轶棂疱蜢轭雹花溴骢棂疱蜢轭氩ㄡ蜱扉篝⑷疱蜢轭氩鏖翳狎珲礤铘狎绌花溴骈铄梏麴骘蝽⑵矧蝈篚祠花溴骈铄梏麴骘蝽⑵矧蝈篚祠花溴骈铄梏麴骘蝽⑵矧蝈篚祠花溴骈铄梏麴骘蝽⑵矧蝈篚祠